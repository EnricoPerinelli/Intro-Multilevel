title: model 4
data: file is ex1_mplus.dat;
variable: names =
ID        !number ID for each employee
organiz   !this is the cluster variable
job_perf  !job performance
boss_exp  !experience of boss
OCB       !organizational citizenship behavior
sex;      !0=boys 1=girls

usevar =
job_perf
boss_exp
OCB;

cluster = organiz;  !this variable defines the cluster
within = OCB;
between = boss_exp; !this defines the between level predictor

define: center OCB (grandmean);
        center boss_exp (grandmean);
               !grand mean centering the independent variables, so to have a meaningful
               !interpetation of the intercept
analysis: type= twolevel random;
!"random" allows the inclusion of a random slope

model: %within%
       beta1j | job_perf on OCB;
       !beta1j is a random name to indicate random slope
       %between%
       job_perf with beta1j;
       job_perf on boss_exp;

output: sampstat stdyx;